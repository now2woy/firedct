plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.github.node-gradle.node" version "3.2.1"
	id 'java'
}

group = 'ji.hs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-batch'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.commons:commons-compress:1.21'
	implementation 'commons-io:commons-io:2.11.0'
	
	implementation 'org.jsoup:jsoup:1.14.3'
	
	compileOnly 'org.projectlombok:lombok'
	
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	
	runtimeOnly 'com.h2database:h2:2.0.206'
	
	annotationProcessor 'org.projectlombok:lombok'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.batch:spring-batch-test'	
}

test {
	useJUnitPlatform()
}


node {
	nodeProjectDir = file("${project.projectDir}/webapp")
}

def yarn = tasks.named("yarn")

def buildTask = tasks.register("buildWebapp", NpxTask) {
	command = "react-scripts"
	args = ["build"]
	dependsOn(yarn)
	inputs.dir(fileTree("webapp/src"))
	inputs.dir("webapp/node_modules")
	outputs.dir("${buildDir}/webapp")
	environment = ["BUILD_PATH": "${project.projectDir}/src/main/resources/static"]
}